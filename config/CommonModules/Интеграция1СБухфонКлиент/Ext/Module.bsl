////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с 1С:Бухфон".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура устанавливает свойства элемента кнопки при встраивании в другие подсистемы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Элемент) Экспорт
	
	Если ИмяСобытия = "СохранениеНастроек1СБухфон" Тогда
		НастройкиПользователя = Интеграция1СБухфонВызовСервера.НастройкиУчетнойЗаписиПользователя();
		Элемент.Видимость = НастройкиПользователя.ВидимостьКнопки1СБухфон;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Диалоговое окно для выбора файла.
//
// Возвращаемое значение:
//		Строка  - Путь к исполняемому файлу.
Функция ВыбратьФайл1СБухфон(ПутьКФайлу = "") Экспорт 
		
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если НЕ РасширениеПодключено Тогда 
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекстПредложения", НСтр("ru = 'Для выбора приложения 1С-Бухфон 
								|необходимо установить расширение работы с файлами.'"));
		ПараметрыФормы.Вставить("ВозможноПродолжениеБезУстановки", Ложь);
		ОткрытьФорму("ОбщаяФорма.ВопросОбУстановкеРасширенияРаботыСФайлами", ПараметрыФормы);
		Возврат "";
	КонецЕсли;		
		
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = НСтр("ru = 'Выберите исполняемый файл 1С-Бухфон'");
	Диалог.ПолноеИмяФайла = ПутьКФайлу;
	Каталог = Новый Файл(ПутьКФайлу);	
	Диалог.Каталог = Каталог.Путь;
	Фильтр = НСтр("ru = 'buhphone.exe (*.exe)|*.exe'");
	Диалог.Фильтр = Фильтр;
	Диалог.МножественныйВыбор = Ложь; 
	
	Если Диалог.Выбрать() Тогда
		Возврат Диалог.ПолноеИмяФайла;
	Иначе               
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Проверяет наличие исполняемого файла по указанному пути.
//
// Возвращаемое значение:
// 		Булево  - При значении Истина файл запустится по указанному пути.
//
Функция НаличиеФайла1СБухфон(Путь) Экспорт
	
	ПроверяемыйФайл = Новый Файл(Путь); 
	Возврат	ПроверяемыйФайл.Существует();
	
КонецФункции

// Возвращает уникальный идентификатор клиента 1С (приложение).
Функция ИдентификаторКлиента() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИдентификаторКлиента = СистемнаяИнформация.ИдентификаторКлиента;
	Возврат ИдентификаторКлиента;
	
КонецФункции

// Возвращает путь файла 1С-Бухфон в реестре Windows.
// 
Функция ПутьКИсполняемомуФайлуИзРеестраWindows() Экспорт
	
	Значение = "";
	
	Если НЕ ЭтоWindowsКлиент() Тогда
		Возврат Значение;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
	    ЗначениеИзРеестра = "";
	#Иначе
		
	RegProv = ПолучитьCOMОбъект("winmgmts:{impersonationLevel=impersonate}!\\.\root\default:StdRegProv");
	RegProv.GetStringValue("2147483649","Software\Buhphone","ProgramPath",Значение);
	
	Если Значение = "" Или  Значение = NULL Тогда
		ЗначениеИзРеестра = "";
	Иначе
		ЗначениеИзРеестра = Значение;
	КонецЕсли;
	
	Возврат ЗначениеИзРеестра;
	#КонецЕсли 
	
КонецФункции

// Возвращает Истина, если клиентское приложение запущено под управлением ОС Linux.
//
// Возвращаемое значение:
//  Булево. Если нет клиентского приложения, возвращается Ложь.
//
Функция ЭтоWindowsКлиент() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	ЭтоWindowsКлиент = СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86
	ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64;
	
	Возврат ЭтоWindowsКлиент;
	
КонецФункции

// Процедура запускает исполняемый файл 1С-Бухфон.
// При отсутствии файла 1С-Бухфон - открывает форму поиска пути к исполняемому файлу.
//
Процедура Запустить1СБухфон() Экспорт
	
	Если НЕ ЭтоWindowsКлиент() Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Для работы с приложением 1С-Бухфон необходима операционная система Microsoft Windows.'"));
		Возврат
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		Оповещение = Новый ОписаниеОповещения("Запустить1СБухфонЗавершение", Интеграция1СБухфонКлиент);
		ТекстСообщения = НСтр("ru = 'Для запуска 1С-Бухфон необходимо установить расширение работы с файлами.'");
		ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Оповещение, ТекстСообщения, Ложь);
	#Иначе 
		Запустить1СБухфонЗавершение();
	#КонецЕсли
	
КонецПроцедуры

// Данная процедура вызывается из процедуры Запустить1СБухфон(Команда) непосредственно
// в тонком/толстом клиенте, или через ОписаниеОповещения после установки расширения
// работы с файлами в веб-клиенте.
// При отсутствии файла 1С-Бухфон - открывает форму поиска пути к исполняемому файлу.
//
Процедура Запустить1СБухфонЗавершение(РасширениеПодключено = Истина, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ РасширениеПодключено Тогда
		Возврат;
	КонецЕсли;
	
	// Определение параметров запуска.
	ИдентификаторКлиента = ИдентификаторКлиента();	
	ПутьИзРеестра = ПутьКИсполняемомуФайлуИзРеестраWindows();
	ПутьИзХранилища = Интеграция1СБухфонВызовСервера.РасположениеИсполняемогоФайла(ИдентификаторКлиента);	
	ПутьИзРеестраВерен = НаличиеФайла1СБухфон(ПутьИзРеестра);
	ПутьИзХранилищаВерен = НаличиеФайла1СБухфон(ПутьИзХранилища);	
	УчетнаяЗапись = Интеграция1СБухфонВызовСервера.НастройкиУчетнойЗаписиПользователя();
	ПараметрыЗапуска1СБухфон = " /StartedFrom1CConf";
	
	Если УчетнаяЗапись.ИспользоватьЛП Тогда 
				
		Если УчетнаяЗапись.Логин <> "" И УчетнаяЗапись.Пароль <> "" Тогда
			ПараметрыЗапуска1СБухфон = ПараметрыЗапуска1СБухфон + " /login:" + УчетнаяЗапись.Логин + " /password:" + УчетнаяЗапись.Пароль;
		КонецЕсли;
		
		Если ПутьИзХранилищаВерен Тогда
			ЗапуститьПриложение(ПутьИзХранилища + ПараметрыЗапуска1СБухфон);
			Возврат;
		КонецЕсли;
		
		Если ПутьИзРеестраВерен Тогда
			ЗапуститьПриложение(ПутьИзРеестра + ПараметрыЗапуска1СБухфон);
			Возврат;
		КонецЕсли;		
		
	Иначе 
		
		Если ПутьИзХранилищаВерен Тогда
			ЗапуститьПриложение(ПутьИзХранилища + ПараметрыЗапуска1СБухфон);
			Возврат;
		КонецЕсли;
		
		Если ПутьИзРеестраВерен Тогда
			ЗапуститьПриложение(ПутьИзРеестра + ПараметрыЗапуска1СБухфон);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ПоискИсполняемогоФайла1СБухфон");
	
КонецПроцедуры

#КонецОбласти





 