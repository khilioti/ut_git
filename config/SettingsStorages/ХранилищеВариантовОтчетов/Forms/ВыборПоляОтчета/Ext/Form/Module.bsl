////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("КомпоновщикНастроек", КомпоновщикНастроек) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""КомпоновщикНастроек"".'");
	КонецЕсли;
	Если Не Параметры.Свойство("Режим", Режим) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""Режим"".'");
	КонецЕсли;
	Если Режим = "СоставГруппировки" Или Режим = "СтруктураВарианта" Тогда
		Режим = "ПоляГруппировки";
	КонецЕсли;
	Если Режим <> "Отборы" И Режим <> "ВыбранныеПоля" И Режим <> "Сортировка" И Режим <> "ПоляГруппировки" Тогда
		ВызватьИсключение СтрЗаменить(НСтр("ru = 'Некорретное значение параметра ""Режим"": ""%1"".'"), "%1", Строка(Режим));
	КонецЕсли;
	Если Не Параметры.Свойство("НастройкиОтчета", НастройкиОтчета) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""НастройкиОтчета"".'");
	КонецЕсли;
	Если Параметры.Свойство("ИдентификаторТекущегоУзлаКД", ИдентификаторТекущегоУзлаКД)
		И ИдентификаторТекущегоУзлаКД <> Неопределено Тогда
		ТекущийУзелКД = КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(ИдентификаторТекущегоУзлаКД);
		Если ТипЗнч(ТекущийУзелКД) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных")
			Или ТипЗнч(ТекущийУзелКД) = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных")
			Или ТипЗнч(ТекущийУзелКД) = Тип("ТаблицаКомпоновкиДанных")
			Или ТипЗнч(ТекущийУзелКД) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			ИдентификаторТекущегоУзлаКД = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Режим = "ПоляГруппировки" Тогда
		ЭлементыДерева = ПоляГруппировки.ПолучитьЭлементы();
		ПоляГруппировкиРазвернутьСтроку(ТаблицаКД(ЭтотОбъект), ЭлементыДерева);
		СтрокаДетальныеЗаписи = ЭлементыДерева.Добавить();
		СтрокаДетальныеЗаписи.Представление = НСтр("ru = '<Детальные записи>'");
		СтрокаДетальныеЗаписи.Картинка      = БиблиотекаКартинок.ПредопределенныйЭлемент;
	КонецЕсли;
	
	ПолеКД = Неопределено;
	Параметры.Свойство("ПолеКД", ПолеКД);
	Если ПолеКД <> Неопределено Тогда
		ТаблицаКД = ТаблицаКД(ЭтотОбъект);
		ДоступноеПолеКД = ТаблицаКД.НайтиПоле(ПолеКД);
		Если ДоступноеПолеКД <> Неопределено Тогда
			Элементы[Режим + "Таблица"].ТекущаяСтрока = ТаблицаКД.ПолучитьИдентификаторПоОбъекту(ДоступноеПолеКД);
		КонецЕсли;
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы[Режим + "Страница"];
	
	Источник = Новый ИсточникДоступныхНастроекКомпоновкиДанных(НастройкиОтчета.АдресСхемы);
	КомпоновщикНастроек.Инициализировать(Источник);
	
	ЗакрыватьПриВыборе = Ложь;
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ЭЛЕМЕНТОВ

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ТАБЛИЦЫ ОтборыТаблица

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборыТаблица

&НаКлиенте
Процедура ОтборыТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ТАБЛИЦЫ ВыбранныеПоляТаблица

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеПоляТаблица

&НаКлиенте
Процедура ВыбранныеПоляТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ТАБЛИЦЫ СортировкаТаблица

#Область ОбработчикиСобытийЭлементовТаблицыФормыСортировкаТаблица

&НаКлиенте
Процедура СортировкаТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ТАБЛИЦЫ ПоляГруппировки

#Область ОбработчикиСобытийЭлементовТаблицыФормыПоляГруппировки

&НаКлиенте
Процедура ПоляГруппировкиТаблицаПередРазворачиванием(ТаблицаЭлемент, ИдентификаторСтроки, Отказ)
	СтрокаДерева = ПоляГруппировки.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Не СтрокаДерева.НадоПрочитатьВложенные Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиДерева = СтрокаДерева.ПолучитьЭлементы();
	СтрокиДерева.Очистить();
	
	ТаблицаКД = ТаблицаКД(ЭтотОбъект);
	ДоступноеПолеКД = ТаблицаКД.ПолучитьОбъектПоИдентификатору(СтрокаДерева.ИдентификаторКД);
	ПоляГруппировкиРазвернутьСтроку(ТаблицаКД, СтрокиДерева, ДоступноеПолеКД);
КонецПроцедуры

&НаКлиенте
Процедура ПоляГруппировкиТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	ТаблицаЭлемент = Элементы[Режим + "Таблица"];
	Если Режим = "ПоляГруппировки" Тогда
		СтрокаДерева = ТаблицаЭлемент.ТекущиеДанные;
		Если СтрокаДерева = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ИдентификаторКД = СтрокаДерева.ИдентификаторКД;
	Иначе
		ИдентификаторКД = ТаблицаЭлемент.ТекущаяСтрока;
	КонецЕсли;
	Если ИдентификаторКД = Неопределено Тогда
		Если Режим = "ПоляГруппировки" Тогда
			ДоступноеПолеКД = "<>";
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		ДоступноеПолеКД = ТаблицаКД(ЭтотОбъект).ПолучитьОбъектПоИдентификатору(ИдентификаторКД);
		Если ДоступноеПолеКД = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ОповеститьОВыборе(ДоступноеПолеКД);
	Закрыть(ДоступноеПолеКД);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Клиент, Сервер

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаКД(ЭтотОбъект)
	Если ЭтотОбъект.Режим = "Отборы" Тогда
		Возврат ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.ДоступныеПоляОтбора;
	ИначеЕсли ЭтотОбъект.Режим = "ВыбранныеПоля" Тогда
		Возврат ЭтотОбъект.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора;
	ИначеЕсли ЭтотОбъект.Режим = "Сортировка" Тогда
		Возврат ЭтотОбъект.КомпоновщикНастроек.Настройки.Порядок.ДоступныеПоляПорядка;
	ИначеЕсли ЭтотОбъект.Режим = "ПоляГруппировки" Тогда
		Если ЭтотОбъект.ИдентификаторТекущегоУзлаКД = Неопределено Тогда
			ТекущийУзелКД = ЭтотОбъект.КомпоновщикНастроек.Настройки;
		Иначе
			ТекущийУзелКД = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(ЭтотОбъект.ИдентификаторТекущегоУзлаКД);
		КонецЕсли;
		Если ТипЗнч(ТекущийУзелКД) = Тип("НастройкиКомпоновкиДанных") Тогда
			Возврат ТекущийУзелКД.ДоступныеПоляГруппировок;
		Иначе
			Возврат ТекущийУзелКД.ПоляГруппировки.ДоступныеПоляПолейГруппировок;
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПоляГруппировкиРазвернутьСтроку(ТаблицаКД, СтрокиДерева, ДоступноеПолеКДРодитель = Неопределено)
	Если ДоступноеПолеКДРодитель = Неопределено Тогда
		ДоступноеПолеКДРодитель = ТаблицаКД;
		Префикс = "";
	Иначе
		Префикс = ДоступноеПолеКДРодитель.Заголовок + ".";
	КонецЕсли;
	
	Для Каждого ДоступноеПолеКД Из ДоступноеПолеКДРодитель.Элементы Цикл
		Если ТипЗнч(ДоступноеПолеКД) = Тип("ДоступноеПолеКомпоновкиДанных") Тогда
			СтрокаДерева = СтрокиДерева.Добавить();
			СтрокаДерева.Представление = СтрЗаменить(ДоступноеПолеКД.Заголовок, Префикс, "");
			СтрокаДерева.ИдентификаторКД = ТаблицаКД.ПолучитьИдентификаторПоОбъекту(ДоступноеПолеКД);
			Если ДоступноеПолеКД.Таблица Тогда
				СтрокаДерева.Картинка = БиблиотекаКартинок.ВложеннаяТаблица;
			ИначеЕсли ДоступноеПолеКД.Ресурс Тогда
				СтрокаДерева.Картинка = БиблиотекаКартинок.Ресурс;
			Иначе
				СтрокаДерева.Картинка = БиблиотекаКартинок.Реквизит;
			КонецЕсли;
			Если ДоступноеПолеКД.Элементы.Количество() > 0 Тогда
				СтрокаДерева.НадоПрочитатьВложенные = Истина;
				СтрокаДерева.ПолучитьЭлементы().Добавить();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти