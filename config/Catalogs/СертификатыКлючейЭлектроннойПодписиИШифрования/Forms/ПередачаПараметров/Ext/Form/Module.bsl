&НаКлиенте
Перем ОбщиеВнутренниеДанные;

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщиеВнутренниеДанные = Новый Соответствие;
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьНовуюФорму(ВидФормы, СерверныеПараметры, КлиентскиеПараметры = Неопределено,
			ОбработкаЗавершения = Неопределено, Знач ВладелецНовойФормы = Неопределено) Экспорт
	
	ВидыФорм = ",ПодписаниеДанных,ШифрованиеДанных,РасшифровкаДанных,
	|,ВыборСертификатаДляПодписанияИлиРасшифровки,ПроверкаСертификата,";
	
	Если Найти(ВидыФорм, "," + ВидФормы + ",") = 0 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка в процедуре ОткрытьНовуюФорму. ВидФормы ""%1"" не поддерживается.'"), ВидФормы);
	КонецЕсли;
	
	Если ВладелецНовойФормы = Неопределено Тогда
		ВладелецНовойФормы = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ОбработкаПродолжения = Новый ОписаниеОповещения("ОткрытьНовуюФорму", ЭтотОбъект);
	
	ИмяНовойФормы = "Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма." + ВидФормы;
	
	Форма = ПолучитьФорму(ИмяНовойФормы, СерверныеПараметры, ВладелецНовойФормы);
	
	Если Форма = Неопределено Тогда
		Если ТипЗнч(ОбработкаЗавершения) = Тип("ОписаниеОповещения") Тогда
			ВыполнитьОбработкуОповещения(ОбработкаЗавершения, Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОбработкаЗавершения) = Тип("ОписаниеОповещения") Тогда
		Форма.ОписаниеОповещенияОЗакрытии = ОбработкаЗавершения;
	КонецЕсли;
	
	Если КлиентскиеПараметры = Неопределено Тогда
		ФормаОткрыта = Форма.ПродолжитьОткрытие(ОбщиеВнутренниеДанные);
	Иначе
		ФормаОткрыта = Форма.ПродолжитьОткрытие(ОбщиеВнутренниеДанные, КлиентскиеПараметры);
	КонецЕсли;
	
	Если Не ФормаОткрыта И ТипЗнч(ОбработкаЗавершения) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОбработкаЗавершения, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПарольСертификата(СертификатСсылка, Пароль, ПояснениеПароля) Экспорт
	
	УстановленныеПароли = ОбщиеВнутренниеДанные.Получить("УстановленныеПароли");
	ПоясненияУстановленныхПаролей = ОбщиеВнутренниеДанные.Получить("ПоясненияУстановленныхПаролей");
	
	Если УстановленныеПароли = Неопределено Тогда
		УстановленныеПароли = Новый Соответствие;
		ОбщиеВнутренниеДанные.Вставить("УстановленныеПароли", УстановленныеПароли);
		ПоясненияУстановленныхПаролей = Новый Соответствие;
		ОбщиеВнутренниеДанные.Вставить("ПоясненияУстановленныхПаролей", ПоясненияУстановленныхПаролей);
	КонецЕсли;
	
	ОбработкаПродолжения = Новый ОписаниеОповещения("УстановитьПарольСертификата", ЭтотОбъект);
	
	УстановленныеПароли.Вставить(СертификатСсылка, Строка(Пароль));
	Пароль = Неопределено;
	
	НовоеПояснениеПароля = Новый Структура;
	НовоеПояснениеПароля.Вставить("ТекстПояснения", "");
	НовоеПояснениеПароля.Вставить("ПояснениеГиперссылка", Ложь);
	НовоеПояснениеПароля.Вставить("ТекстПодсказки", "");
	НовоеПояснениеПароля.Вставить("ОбработкаДействия", Неопределено);
	
	Если ТипЗнч(ПояснениеПароля) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(НовоеПояснениеПароля, ПояснениеПароля);
	КонецЕсли;
	
	ПоясненияУстановленныхПаролей.Вставить(СертификатСсылка, НовоеПояснениеПароля);
	
КонецПроцедуры

#КонецОбласти
