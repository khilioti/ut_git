
Процедура Печать(ТабДок, Ссылка) Экспорт
	
	ДокСсылка = Ссылка[0];	
	
	Макет1 = Документы.РезультатыПроверкиМагазинаРД.ПолучитьМакет("Печать");
	
	Обл = Макет1.ПолучитьОбласть("Шапка");
	Обл.Параметры.Заполнить(ДокСсылка);
	ТабДок.Вывести(Обл);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Ссылка,
	               |	ВложенныйЗапрос.Родитель,
				   |	ВложенныйЗапрос.Родитель.Родитель КАК ГлРодитель,
	               |	ВложенныйЗапрос.ЭтоГруппа,
	               |	ВложенныйЗапрос.ПолнНаименование,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.НомерПоПорядку > 1000
	               |			ТОГДА ВложенныйЗапрос.НомерПоПорядку
	               |		ИНАЧЕ ВложенныйЗапрос.РодительРодительНомерПоПорядку * 100000 + ВложенныйЗапрос.РодительНомерПоПорядку * 1000 + ВложенныйЗапрос.НомерПоПорядку
	               |	КОНЕЦ КАК ОбщийНомерПоПорядку,
	               |	ВложенныйЗапрос.НомерПоПорядку,
	               |	ВложенныйЗапрос.РодительНомерПоПорядку,
	               |	ВложенныйЗапрос.РодительРодительНомерПоПорядку,
	               |	СУММА(ВложенныйЗапрос.Кво) КАК Кво,
	               |	МАКСИМУМ(ВложенныйЗапрос.КомментарииПроверяющего) КАК КомментарииПроверяющего,
	               |	МАКСИМУМ(ВложенныйЗапрос.СрокУстранения) КАК СрокУстранения,
	               |	МАКСИМУМ(ВложенныйЗапрос.ОтметкаОбИсполнении) КАК ОтметкаОбИсполнении,
				   |	МАКСИМУМ(ВложенныйЗапрос.КомментарийИсполнителя) КАК КомментарийИсполнителя,
	               |	МАКСИМУМ(ВложенныйЗапрос.ЗадачаУправляющемуСсылка) КАК ЗадачаУправляющемуСсылка
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		КритерииПроверкиМагазинов.Ссылка КАК Ссылка,
	               |		КритерииПроверкиМагазинов.Родитель КАК Родитель,
	               |		КритерииПроверкиМагазинов.ЭтоГруппа КАК ЭтоГруппа,
	               |		КритерииПроверкиМагазинов.ПолнНаименование КАК ПолнНаименование,
	               |		ВЫБОР
	               |			КОГДА НЕ КритерииПроверкиМагазинов.ЭтоГруппа
	               |				ТОГДА КритерииПроверкиМагазинов.НомерПоПорядку
	               |			ИНАЧЕ ВЫБОР
	               |					КОГДА КритерииПроверкиМагазинов.Родитель.Ссылка ЕСТЬ NULL 
	               |						ТОГДА 100000 * КритерииПроверкиМагазинов.НомерПоПорядку
	               |					ИНАЧЕ 100000 * КритерииПроверкиМагазинов.Родитель.НомерПоПорядку + 1000 * КритерииПроверкиМагазинов.НомерПоПорядку
	               |				КОНЕЦ
	               |		КОНЕЦ КАК НомерПоПорядку,
	               |		ВЫБОР
	               |			КОГДА КритерииПроверкиМагазинов.Родитель.Ссылка ЕСТЬ NULL 
	               |				ТОГДА 0
	               |			ИНАЧЕ КритерииПроверкиМагазинов.Родитель.НомерПоПорядку
	               |		КОНЕЦ КАК РодительНомерПоПорядку,
	               |		ВЫБОР
	               |			КОГДА КритерииПроверкиМагазинов.Родитель.Родитель.Ссылка ЕСТЬ NULL 
	               |				ТОГДА 0
	               |			ИНАЧЕ КритерииПроверкиМагазинов.Родитель.Родитель.НомерПоПорядку
	               |		КОНЕЦ КАК РодительРодительНомерПоПорядку,
	               |		0 КАК Кво,
	               |		"""" КАК КомментарииПроверяющего,
	               |		&ПустДата КАК СрокУстранения,
	               |		ЛОЖЬ КАК ОтметкаОбИсполнении,
				   |		"""" КАК КомментарийИсполнителя,
	               |		0 КАК ЗадачаУправляющемуСсылка
	               |	ИЗ
	               |		Справочник.КритерииПроверкиМагазиновРД КАК КритерииПроверкиМагазинов
	               |	ГДЕ
	               |		НЕ КритерииПроверкиМагазинов.ПометкаУдаления
	               |	
	               |	ОБЪЕДИНИТЬ
	               |	
	               |	ВЫБРАТЬ
	               |		РезультатыПроверкиМагазинаРезультаты.Критерий,
	               |		РезультатыПроверкиМагазинаРезультаты.Критерий.Родитель,
	               |		РезультатыПроверкиМагазинаРезультаты.Критерий.ЭтоГруппа,
	               |		РезультатыПроверкиМагазинаРезультаты.Критерий.ПолнНаименование,
	               |		РезультатыПроверкиМагазинаРезультаты.Критерий.НомерПоПорядку,
	               |		ВЫБОР
	               |			КОГДА РезультатыПроверкиМагазинаРезультаты.Критерий.Родитель.Ссылка ЕСТЬ NULL 
	               |				ТОГДА 0
	               |			ИНАЧЕ РезультатыПроверкиМагазинаРезультаты.Критерий.Родитель.НомерПоПорядку
	               |		КОНЕЦ,
	               |		ВЫБОР
	               |			КОГДА РезультатыПроверкиМагазинаРезультаты.Критерий.Родитель.Родитель.Ссылка ЕСТЬ NULL 
	               |				ТОГДА 0
	               |			ИНАЧЕ РезультатыПроверкиМагазинаРезультаты.Критерий.Родитель.Родитель.НомерПоПорядку
	               |		КОНЕЦ,
	               |		РезультатыПроверкиМагазинаРезультаты.Кво,
	               |		РезультатыПроверкиМагазинаРезультаты.КомментарииПроверяющего,
	               |		РезультатыПроверкиМагазинаРезультаты.СрокУстранения,
	               |		РезультатыПроверкиМагазинаРезультаты.ОтметкаОбИсполнении,
				   |		РезультатыПроверкиМагазинаРезультаты.КомментарийИсполнителя,
	               |		ВЫБОР
	               |			КОГДА РезультатыПроверкиМагазинаРезультаты.ЗадачаУправляющему ЕСТЬ NULL 
	               |				ТОГДА 0
	               |			ИНАЧЕ РезультатыПроверкиМагазинаРезультаты.ЗадачаУправляющему
	               |		КОНЕЦ
	               |	ИЗ
	               |		Документ.РезультатыПроверкиМагазинаРД.Результаты КАК РезультатыПроверкиМагазинаРезультаты
	               |	ГДЕ
	               |		РезультатыПроверкиМагазинаРезультаты.Ссылка = &ВыбДокумент) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Ссылка,
	               |	ВложенныйЗапрос.Родитель,
	               |	ВложенныйЗапрос.ЭтоГруппа,
	               |	ВложенныйЗапрос.ПолнНаименование,
	               |	ВложенныйЗапрос.НомерПоПорядку,
	               |	ВложенныйЗапрос.РодительНомерПоПорядку,
	               |	ВложенныйЗапрос.РодительРодительНомерПоПорядку
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОбщийНомерПоПорядку";
				   
				   
	Запрос.УстановитьПараметр("ВыбДокумент", Ссылка[0]);
	Запрос.УстановитьПараметр("ПустДата", Дата(2010,1,1));

	
	тз = Запрос.Выполнить().Выгрузить();
	//тз.Сортировать("РодительРодительНомерПоПорядку, РодительНомерПоПорядку, НомерПоПорядку");
	

	Нпп = 0;
	Для Каждого стр из тз цикл
		Если стр.ЭтоГруппа тогда
			Если ЗначениеЗаполнено(стр.Родитель) тогда  // подгруппа
				
				ПараметрыОтбора = Новый Структура("Родитель",стр.Ссылка);
				МассивСтрок = тз.НайтиСтроки(ПараметрыОтбора);
				СуммаГруппы = 0;
				Для Каждого эл Из МассивСтрок Цикл
					СуммаГруппы = СуммаГруппы + эл.Кво;
				КонецЦикла;
				
				Обл = Макет1.ПолучитьОбласть("Группа");
				Обл.Параметры.СтрГруппа = стр.ПолнНаименование;
				Обл.Параметры.Родитель = стр.Родитель;
				Обл.Параметры.БалГруппа = СуммаГруппы;
				ТабДок.Вывести(Обл);

			иначе  // раздел		
				
				ПараметрыОтбора = Новый Структура("ГлРодитель",стр.Ссылка);
				МассивСтрок = тз.НайтиСтроки(ПараметрыОтбора);
				СуммаРаздела = 0;
				Для Каждого эл Из МассивСтрок Цикл
					СуммаРаздела = СуммаРаздела + эл.Кво;
				КонецЦикла;
	
				Обл = Макет1.ПолучитьОбласть("Раздел");
				Обл.Параметры.СтрРаздел = стр.ПолнНаименование;
				Обл.Параметры.Родитель = "Сам";
				Обл.Параметры.БалРаздел = СуммаРаздела;
				ТабДок.Вывести(Обл);

			КонецЕсли;
			
		иначе // строка
			
			Нпп = Нпп + 1;
			Обл = Макет1.ПолучитьОбласть("Строка");
			Обл.Параметры.Нпп = Нпп;
			Обл.Параметры.СтрНаим = стр.ПолнНаименование;
			Обл.Параметры.ТекКритерий = стр.Ссылка;
			Обл.Параметры.СтрБал = стр.Кво;		 
			Обл.Параметры.СтрКоммент = стр.КомментарииПроверяющего;	
			Обл.Параметры.Родитель = стр.Родитель;

			Если стр.СрокУстранения <> Дата(2010,1,1) тогда
				Обл.Параметры.СтрДат = стр.СрокУстранения;
			КонецЕсли;
			ТабДок.Вывести(Обл);
			ТабДок.Область("R"+ТабДок.ВысотаТаблицы+"C3").Формат = "ЧН=0";
		КонецЕсли;
	КонецЦикла;
	ТабДок.ФиксацияСверху = 8;
	ОблПусто = Макет1.ПолучитьОбласть("Пусто");
	ТабДок.Вывести(ОблПусто);
	КолвоКритериев = Нпп;	
КонецПроцедуры
